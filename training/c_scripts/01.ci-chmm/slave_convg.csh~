#!/bin/csh -f

source ../variables.def

#***************************************************************************
# This script launches all the ci - continuous training jobs in the proper
# order. First it cleans up the directories, then launches the 
# flat initialization, and the baum-welch and norm jobs for the required
# number of iterations. Within each iteration it launches as many baumwelch
# jobs as the number of parts we wish to split the training into.
#***************************************************************************

if ($#argv > 1) then
    echo "USAGE: $0 <iteration number (def 1)>"
    exit 0
endif

if ($#argv == 0) then
    echo "USAGE: $0 <iteration number (def 1)>"
    echo "Setting iter value to 1"
    echo -n "Continue? (y/n) "
    set ans = $<
    if ($ans != "y") exit 0
    set iter = 1
else
    set iter = $1
endif

set scriptdir = $base_dir/c_scripts/01.ci-chmm

set qmgrdir   = $base_dir/qmanager
if ( ! -e $qmgrdir ) mkdir -p $qmgrdir
set logdir = $base_dir/logdir/01.ci_chmm
if ( ! -e $logdir ) mkdir -p $logdir        
set bwaccumdir = $base_dir/bwaccumdir
set modeldir  = $base_dir/model_parameters
if ( ! -e $bwaccumdir ) mkdir -p $bwaccumdir
if ( ! -e $modeldir ) mkdir -p $modeldir


echo "Cleaning up accumulator directories..."
/bin/rm  -f $bwaccumdir/${exptname}_buff_?/* $bwaccumdir/${exptname}_buff_??/*

if ($iter == 1) then
    echo "Cleaning up log directories..."
    /bin/rm -f $logdir/*
    echo "Cleaning up qmanager directories..."
    /bin/rm -f $qmgrdir/*.err
    /bin/rm -f $qmgrdir/*.out
    /bin/rm -f $qmgrdir/*
    echo "Cleaning up model directories.."
    /bin/rm -f $modeldir/${exptname}.ci_continuous/*
endif


    set errfn = $qmgrdir/$exptname.initialize_ci.err
    set outfn = $qmgrdir/$exptname.initialize_ci.out


if ( $iter == 1 ) then
$scriptdir/flat_initialize.csh
echo "initialization done..."
endif 

foreach part (`../../../tools/interval 1 $npart`)
$scriptdir/baum_welch.csh $iter $part $npart
echo baum_welch $iter $part done...
end
echo baum_welch $iter done...

echo launching norm for $iter...
$scriptdir/norm_and_launchbw.csh $iter


exit 0

